# This graph loads a list of documents from a directory and sends it out 
# for processing.
#
# The directory inport accepts a path for a folder where to start loading
EXPORT=DIRECTORY.IN:DIRECTORY
# The subdirectory outport is where the path each subfolder is sent
EXPORT=SUBDIRECTORY.OUT:SUBDIRECTORY
# The document outport is where the path of each regular file is sent
EXPORT=FILENAME.OUT:DOCUMENT

Directory(Split) OUT -> SOURCE Dir(ReadDir)
Dir() OUT -> IN Stat(Stat)

# Routing rules
'isDirectory' -> ROUTES FileRouter(GroupRouter)
'isDirectory' -> KEY Group(GroupByObjectKey)
Stat() OUT -> IN Group() OUT -> IN FileRouter()

# First route: subfolder
'path=^(?:(?!\.git).)*$' -> REGEXP FilterDirs(FilterPropertyValue)
'path' -> KEY SubDirectory(GetObjectKey)
FileRouter() OUT -> IN FilterDirs() OUT -> IN SubDirectory()

# Missed routing: regular file
'path' -> KEY FileName(GetObjectKey)
FileRouter() MISSED -> IN FileName()

# Error handling
Dir() ERROR -> IN Display(Output)
Stat() ERROR -> IN Display(Output)
