EXPORT=SOURCE.IN:SOURCE
EXPORT=DESTINATION.IN:DESTINATION
EXPORT=MAPFILENAME.IN:IN
EXPORT=SAVE.OUT:OUT
EXPORT=ERRORS.OUT:ERROR

'=' -> DELIMITER CompileReplacement(CompileString)
Source(Split) OUT -> IN CompileReplacement()
Destination(Split) OUT -> IN CompileReplacement()
CompileReplacement() OUT -> REGEXP MapFileName(MapGroup)

# Convert some filename endings
# TODO: Move to main control
'.md=.html' -> REGEXP MapFileName()
'.markdown=.html' -> REGEXP MapFileName()

# Get the new filename
MapFileName() OUT -> IN ReadPath(ReadGroup)

# Create folders for the files
ReadPath() GROUP -> IN NewDir(DirName)
NewDir() OUT -> IN MakeDir(MakeDir)

# Save files
# First connection is filename, second is file contents
MakeDir() OUT -> IN Merge(MergeGroups)
ReadPath() OUT -> IN Merge(MergeGroups)
Merge() OUT -> IN ReadWithDir(ReadGroup) GROUP -> FILENAME Save(WriteFile)
'1' -> KEY GetContents(GetObjectKey)
Merge() OUT -> IN GetContents() OUT -> IN Save(WriteFile)

# Error handling
MakeDir() ERROR -> IN Errors(Merge)
Save() ERROR -> IN Errors(Merge)
